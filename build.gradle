buildscript {
    ext  {
        mavenRepos = {
            mavenLocal()
            maven { url "https://plugins.gradle.org/m2/" }
            maven { url "https://repo.grails.org/grails/core" }
            mavenCentral()
            maven {
                credentials {
                    username mavenUser
                    password mavenPassword
                }
                url "http://maven.causecode.com"
            }
        }
    }

    repositories mavenRepos
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "com.causecode.plugins:gradle-code-quality:0.0.8"
    }
}

version "0.0.9"
group "com.causecode.plugins"

apply plugin:"idea"
apply plugin:"org.grails.grails-plugin"
apply plugin:"org.grails.grails-plugin-publish"
apply plugin:'gradle-code-quality'

repositories mavenRepos

dependencyManagement {
    imports {
        mavenBom "org.grails:grails-bom:$grailsVersion"
    }
    applyMavenExclusions false
}

dependencies {
    // Default plugin dependencies shipped with grails 3.2.0
    compile "org.springframework.boot:spring-boot-starter-logging"
    compile "org.springframework.boot:spring-boot-autoconfigure"
    compile "org.grails:grails-core"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile "org.springframework.boot:spring-boot-starter-tomcat"
    compile "org.grails:grails-dependencies"
    compile "org.grails:grails-web-boot"
    compile "org.grails.plugins:cache"
    compile "org.grails.plugins:scaffolding"
    profile "org.grails.profiles:web-plugin"
    provided "org.grails:grails-plugin-services"
    provided "org.grails:grails-plugin-domain-class"
    testCompile "org.grails:grails-plugin-testing"

    // CauseCode Dependencies
    provided "com.causecode.plugins:nucleus:0.5.0"
    provided "com.causecode.plugins:rest-security-core:0.0.1"
    // Grails JSON Views
    provided "org.grails.plugins:views-json:1.1.3"
    provided "org.grails.plugins:views-json-templates:1.1.3"

    // Grails Plugins
    provided 'org.grails.plugins:quartz:2.0.9'
    provided "org.grails.plugins:asynchronous-mail:2.0.0.RC4"
    provided "org.grails.plugins:spring-security-core:3.1.1"

    // Test Dependencies
    testCompile "org.grails:grails-datastore-rest-client"

    // Hibernate Dependencies
    provided "org.grails.plugins:hibernate5"

    // In memory H2 Database.
    runtime "com.h2database:h2"
}

ccJacocoExt {
    excludes = [
            '**/Application*/**',
            '**/UrlMappings*/**',
            // CustomOauthUserDetailsService 3rd party methods.
            '**/CustomOauthUserDetailsService*/**',
            '**/UserManagementUrlMappings*/**',
            '**/UserManagementGrailsPlugin*/**',
            '**/AuthenticationTokenCleanUpJob*/**',
            '**/UserDomainMarshaller*/**',
            '**/UserManagementBootStrap*/**'
    ]
}

project.publishing {
    publications {
        mavenJava(MavenPublication) {
            from project.components.java

            artifactId "user-management"

            pom.withXml {
                def pomNode = asNode()
                // version-less dependencies are handled with dependencyManagement
                pomNode.dependencies.dependency.findAll {
                    it.version.text().isEmpty()
                }.each {
                    it.replaceNode {}
                }
            }
        }
    }
    repositories {
        maven {
            credentials {
                username mavenPublishUser
                password mavenPublishPassword
            }
            url "http://maven-upload.causecode.com"
        }
    }
}

grailsPublish {
    user = 'user'  // Not required if you defined BINARY_USER var
    key = 'key'    // Not required if you defined BINARY_KEY var
    userOrg = 'mycompany' // Optional
    githubSlug = 'foo/bar'
    license {
        name = 'Apache-2.0'
    }
    title = "My Plugin Title"
    desc = "My Plugin Description"
    developers = [johndoe: "John Doe"]
}

/*
 * This sourceSets block is required for the Grails Database Migration Plugin.
 * Refer https://github.com/grails-plugins/grails-database-migration
 */
sourceSets {
    main {
        resources {
            srcDir 'grails-app/migrations'
        }
    }
}

