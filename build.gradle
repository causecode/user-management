buildscript {
    ext  {
        mavenRepos = {
            mavenLocal()
            maven { url "https://plugins.gradle.org/m2/" }
            maven { url "https://repo.grails.org/grails/core" }
            mavenCentral()
        }
    }

    repositories mavenRepos
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "org.grails.plugins:hibernate5:${gormVersion-".RELEASE"}"
        classpath 'org.grails.plugins:views-gradle:1.2.7'
    }
}

version "1.0.3"

group "com.causecode.plugins"

apply plugin:"idea"
apply plugin:"org.grails.grails-plugin"
apply plugin: 'maven-publish'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'jacoco'

repositories mavenRepos

dependencyManagement {
    imports {
        mavenBom "org.grails:grails-bom:$grailsVersion"
    }
    applyMavenExclusions false
}

dependencies {
    // Default plugin dependencies shipped with grails 3.2.0
    compile "org.springframework.boot:spring-boot-starter-logging"
    compile "org.springframework.boot:spring-boot-autoconfigure"
    compile "org.grails:grails-core"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile "org.springframework.boot:spring-boot-starter-tomcat"
    compile "org.grails:grails-web-boot"
    compile "org.grails:grails-logging"
    compile "org.grails:grails-plugin-rest"
    compile "org.grails:grails-plugin-databinding"
    compile "org.grails:grails-plugin-i18n"
    compile "org.grails:grails-plugin-services"
    compile "org.grails:grails-plugin-url-mappings"
    compile "org.grails:grails-plugin-interceptors"
    compile "org.grails.plugins:cache"
    compile "org.grails.plugins:async"
    compile "org.grails.plugins:scaffolding"

    runtime "org.apache.tomcat:tomcat-jdbc"

    // CauseCode Dependencies
    provided "com.causecode.plugins:nucleus:0.5.6"
    // Grails JSON Views
    provided "org.grails.plugins:views-json:1.1.3"
    provided "org.grails.plugins:views-json-templates:1.1.3"

    // Grails Plugins
    provided 'org.grails.plugins:quartz:2.0.13'
    compile 'org.grails.plugins:spring-security-core:3.2.1'
    provided "javax.validation:validation-api:1.1.0.Final"
    compile "org.grails.plugins:asynchronous-mail:2.0.2"
    provided "org.grails.plugins:spring-security-rest:2.0.0.M2"

    // Test Dependencies
    testCompile "org.grails:grails-datastore-rest-client"
    testRuntime "org.grails.plugins:mongodb"

    testCompile "org.grails:grails-gorm-testing-support"
    compile "org.grails:grails-test-mixins:3.3.0"
    testCompile "org.grails:grails-web-testing-support"

    // Hibernate Dependencies
    provided "org.grails.plugins:hibernate5"

    // In memory H2 Database.
    runtime "com.h2database:h2"
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.enabled true
        html.destination "${buildDir}/jacoco/jacocoHtml/"
        xml.destination "${buildDir}/jacoco/jacocoXml/jacoco.xml"
    }

    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: [
                            '**/Application*/**',
                            '**/UrlMappings*/**',
                            // CustomOauthUserDetailsService 3rd party methods.
                            '**/CustomOauthUserDetailsService*/**',
                            '**/UserManagementUrlMappings*/**',
                            '**/UserManagementGrailsPlugin*/**',
                            '**/AuthenticationTokenCleanUpJob*/**',
                            '**/UserDomainMarshaller*/**',
                            '**/UserManagementBootStrap*/**'
                    ])
        })
    }

    executionData test
    dependsOn test
}

artifacts {
    archives jar

    archives javadocJar
    archives sourcesJar
}

signing {
    useGpgCmd()
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'user-management'
                packaging 'jar'
                description 'This Plugin provides endpoints for User related operations.'
                url 'https://github.com/causecode/user-management'

                scm {
                    url 'scm:svn:https://github.com/causecode/user-management'
                    connection 'scm:svn:https://github.com/causecode/user-management.git'
                    developerConnection 'https://github.com:causecode/user-management.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'causecode'
                        name 'CauseCode Technologies'
                        email 'bootstrap@causecode.com'
                    }
                }
            }
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifactId "user-management"

            pom.withXml {
                def pomNode = asNode()
                // version-less dependencies are handled with dependencyManagement
                pomNode.dependencies.dependency.findAll {
                    it.version.text().isEmpty()
                }.each {
                    it.replaceNode {}
                }
            }
        }
    }
}

/*
 * This sourceSets block is required for the Grails Database Migration Plugin.
 * Refer https://github.com/grails-plugins/grails-database-migration
 */
sourceSets {
    main {
        resources {
            srcDir 'grails-app/migrations'
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = gradleWrapperVersion
}
